buildscript {
    ext {
        springBootVersion = '1.4.2.RELEASE'
    }
    repositories {
        mavenCentral()
        maven { url 'http://repo.spring.io/plugins-release' }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath('org.springframework.build.gradle:propdeps-plugin:0.0.7')
        classpath('com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7')
    }
}

group 'me.bayes'
version '1.0.1'

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'propdeps'
apply plugin: 'propdeps-maven'
apply plugin: 'propdeps-idea'
apply plugin: 'propdeps-eclipse'
apply plugin: 'com.jfrog.bintray'


sourceCompatibility = 1.8
targetCompatibility = 1.8

jar {
    baseName = 'vertx-spring-boot-starter'
    version = '1.0.1'
}

repositories {
    mavenCentral()
}

bintray {
    user = System.getenv("bin_user")
    key = System.getenv("bin_pass")
    configurations = ['archives']
    pkg {
        repo = 'generic'
        name = 'bayes-release'
        licenses = ['Apache-2.0']
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            name = 'Kevin Bayes Public Release Repository'
            repository(id: 'bayes-release', url: 'https://api.bintray.com/maven/kevinbayes/bayes-release/vertx-spring-boot-starter') {
                authentication(userName: System.getenv("bin_user"), password: System.getenv("bin_pass"))
            }
        }
    }
}

dependencies {

    compile("org.springframework.boot:spring-boot-starter:${springBootVersion}")
    compile("io.vertx:vertx-core:3.3.3")

    testCompile("org.springframework.boot:spring-boot-starter-test:${springBootVersion}")

    optional("org.springframework.boot:spring-boot-configuration-processor:${springBootVersion}")
}

compileJava.dependsOn(processResources)

task writeNewPom << {
    pom {
        project {
            groupId 'me.bayes'
            artifactId 'vertx-spring-boot-starter'
            version '1.0.1'

            inceptionYear '2016'
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
        }
    }.writeTo("$buildDir/classes/main/META-INF/maven.me.bayes.vertx.spring.boot.vertx-spring-boot-starter/pom.xml")
}


task wrapper(type: Wrapper) {
    gradleVersion = '3.1'
}